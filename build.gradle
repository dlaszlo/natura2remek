group 'hu.dlaszlo'
version '1.0'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE'
    }
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'application'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:Cairo-SR1'
    }
}

dependencies {
    compile 'org.apache.commons:commons-csv:1.5'
    compile 'commons-cli:commons-cli'
    compile 'org.apache.commons:commons-lang3'
    compile 'commons-beanutils:commons-beanutils'

    compile 'javax.validation:validation-api'
    compile 'org.hibernate.validator:hibernate-validator'
    compile 'org.hibernate.validator:hibernate-validator-annotation-processor'
    compile 'javax.el:javax.el-api'
    compile 'org.glassfish.web:javax.el:2.2.6'

    compile 'org.slf4j:slf4j-api'
    compile 'ch.qos.logback:logback-classic'

    compile 'com.google.inject:guice:4.2.0'

    compile 'com.miglayout:miglayout-javafx:5.1'

    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    compile 'com.fasterxml.jackson.core:jackson-databind'

    testCompile 'junit:junit:4.12'
}

jar {
    exclude 'kontir.yml'
    exclude 'logback.xml'
}

mainClassName = 'hu.dlaszlo.natura2remek.gui.Natura2RemekGui'

applicationDefaultJvmArgs =
[
        "-Xmx3G",
        "-Dfile.encoding=ISO-8859-2",
        "-Dlogback.configurationFile=MY_APP_HOME/config/logback.xml",
        "-Dconfig.file=MY_APP_HOME/config/kontir.yml",
        "-Dlog.directory=MY_APP_HOME/logs"
]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('MY_APP_HOME', '\$APP_HOME')
        windowsScript.text = windowsScript.text.replace('MY_APP_HOME', '%~dp0..')
    }
}
